5 IF (L=0) THEN L=1: LOAD "BITMAP",8,1 
10 FOR X=16192 TO 16192+63: READ Y: POKE X,Y: NEXT X: REM READ BALL SPRITE
20 FOR X=16256 TO 16256+63: READ Y: POKE X,Y: NEXT X: REM READ PADDLE SPRITE
30 POKE 2040,253: REM SET BALL SPRITE POINTER
40 POKE 2041,254: REM SET PADDLE SPRITE POINTER
50 B=1: REM BALL BIT
60 P=2: REM PADDEL BIT
70 PO=0: REM POINTS
80 GOSUB 6000: REM PRINT BACKGROUND
120 POKE 53276,B+P: REM SET SPRITES MULTICOLOR MODE
130 POKE 53277,P: REM SET PADDLE DOUBLE WIDTH
150 POKE 53285,0: REM SET ALL SPRITES BORDER COLOR
160 X=RND(-TI): REM RND INITIALIZATION
170 POKE 53287,2: REM SET BALL SPRITE COLOR
180 POKE 53288,9: REM SET PADDLE SPRITE COLOR
190 POKE 54272,50: REM SET FREQUENCY VOICE 1 LOW BYTE
200 POKE 54273,70: REM SET FREQUENCY VOICE 1 HIGH BYTE
210 POKE 54277,0: REM SET ATTACK AND DECAY DURATION VOICE 1
220 POKE 54278,247: REM SET SUSTAIN LEVEL AND RELEASE DURATION
230 POKE 54296,15: REM SET FILTER MODE AND MAIN VOLUME CONTROL
240 CR=53278: REM SPRITES COLLISIONS REGISTER
250 ER=53264: REM SPRITES X COORDINATE EXTRA BITS REGISTER
260 POKE ER,0: REM CLEAR SPRITES X COORDINATE EXTRA BITS REGISTER
270 SR=53248: REM SPRITES COORDINATES REGISTERS
280 BR=SR: REM BALL COORDINATES REGISTERS
290 PR=SR+2: REM PADDLE COORDINATES REGISTERS
300 POKE BR,(RND(1)*200)+50: REM SET BALL X COORDINATE
310 POKE BR+1,(RND(1)*100)+50: REM SET BALL Y COORDINATE
320 POKE PR,(RND(1)*200)+50: REM SET PADDLE X COORDINATE
330 POKE PR+1,233: REM SET PADDLE Y COORDINATE
340 POKE 53269,B+P: REM ENABLE SPRITES
350 X=PEEK(CR): REM CLEAR SPRITES COLLISIONS REGISTER
360 BH=INT(RND(1)*2): REM SET BALL HORIZONTAL MOVEMENT
370 BV=INT(RND(1)*2): REM SET BALL VERTICAL MOVEMENT
380 PH=INT(RND(1)*2): REM SET PADDLE HORIZONTAL MOVEMENT
390 IF (BH=0) THEN BH=-1
400 IF (BV=0) THEN BV=-1
410 IF (PH=0) THEN PH=-1
420 FOR X=0 TO 1 STEP 0
430 BX=PEEK(BR): REM READ BALL X COORDINATE
440 BY=PEEK(BR+1): REM READ BALL Y COORDINATE
450 BE=PEEK(ER) AND B: REM READ BALL X COORDINATE EXTRA BIT
460 PX=PEEK(PR): REM READ PADDLE X COORDINATE
470 PE=PEEK(ER) AND P: REM READ PADDLE X COORDINATE EXTRA BIT
480 IF ((PEEK(CR) AND B+P) AND BV=1) THEN GOSUB 5000: REM DETECT COLLISION
490 IF (BE=0 AND BX=20) THEN BH=1 
500 IF (BE=B AND BX=68) THEN BH=-1
510 IF (BY=47) THEN BV=1
520 IF (BY=231) THEN GOTO 690
530 IF (BX=0 AND BH=-1) THEN GOSUB 1000: GOTO 560
540 IF (BX=255 AND BH=1) THEN GOSUB 2000: GOTO 560
550 POKE BR,BX+BH: REM UPDATE BALL X COORDINATE
560 POKE BR+1,BY+BV: REM UPDATE BALL Y COORDINATE
570 IF (PE=0 AND PX=24) THEN PH=1 
580 IF (PE=P AND PX=40) THEN PH=-1
590 IF (PX=0 AND PH=-1) THEN GOSUB 3000: GOTO 620
600 IF (PX=255 AND PH=1) THEN GOSUB 4000: GOTO 620
610 POKE PR,PX+PH: REM UPDATE PADDLE X COORDINATE
620 GET K$: REM READ KEY PRESS
630 IF (K$="J") THEN PH=-1
640 IF (K$="K") THEN PH=1
650 J1=PEEK(56321): REM READ JOYSTICK 1 REGISTER
660 IF (J1 AND 4)=0 THEN PH=-1
670 IF (J1 AND 8)=0 THEN PH=1
680 NEXT X
690 FOR X=1612 TO 1621: POKE X,0: NEXT X: REM SHOW GAME OVER MESSAGE
710 END
1000 POKE ER,PEEK(ER) - B: POKE BR,255: RETURN: REM DISABLE BALL EXTRA BIT
2000 POKE ER,PEEK(ER) + B: POKE BR,0: RETURN: REM ENABLE BALL EXTRA BIT
3000 POKE ER,PEEK(ER) - P: POKE PR,255: RETURN: REM DISABLE PADDLE EXTRA BIT
4000 POKE ER,PEEK(ER) + P: POKE PR,0: RETURN: REM ENABLE PADDLE EXTRA BIT
5000 POKE 54276,32+1: REM START SOUND
5010 BV=-1
5020 BT=BX: PT=PX: REM COPY X COORDINATES
5030 IF (BE=B) THEN BT=256+BT: REM BALL TOTAL X COORDINATE
5040 IF (PE=P) THEN PT=256+PT: REM PADDEL TOTAL X COORDINATE
5050 SD=BT-PT: REM SPRITES DISTANCE
5060 IF (SD>35) THEN BH=1
5070 IF (SD<-11) THEN BH=-1
5090 POKE 54276,32: REM STOP SOUND
5100 GOSUB 7000
5110 RETURN
6000 REM ******************** PRINT BACKGROUND ******************** 
6010 POKE 53265,PEEK(53265) AND NOT 16: REM SET SCREEN NOT VISIBLE
6020 POKE 53265,PEEK(53265) OR 32: REM SET HIGH RESOLUTION GRAPHICS
6030 POKE 53270,PEEK(53270) OR 16: REM SET MULTICOLOR MODE
6040 POKE 53272,PEEK(53272) OR 8: REM SET BITMAP STAR ADDRESS AT 8192
6050 LM=24576: REM LOAD MEMORY ADDRESS
6060 FOR X=0 TO 7999: POKE 8192+X,PEEK(LM+X): NEXT X: LM=LM+8000
6070 FOR X=0 TO 999: POKE 1024+X,PEEK(LM+X): NEXT X: LM=LM+1000
6080 FOR X=0 TO 999: POKE 55296+X,PEEK(LM+X): NEXT X: LM=LM+1000
6090 POKE 53281,PEEK(LM): REM SET BACKGROUND COLOR
6100 FOR X=1612 TO 1621: POKE X,3: NEXT X: REM HIDE GAME OVER MESSAGE
6110 POKE 53265,PEEK(53265) OR 16: REM SET SCREEN VISIBLE
6120 FOR X=0 TO 9: FOR Y=O TO 7: READ Z: N(X,Y)=Z: NEXT Y: NEXT X
6130 RETURN
7000 REM ********************   PRINT POINTS   ********************
7010 PO=PO+1: N1=PO
7020 FOR X=3 TO 0 STEP -1
7030 N2=INT(N1/10)
7040 N3=N1-(N2*10)
7050 NM=8576+(X*8)
7060 FOR Y=0 TO 7: POKE NM+Y,N(N3,Y): NEXT Y
7070 IF N3>0 THEN GOTO 7100
7080 N1=N2
7090 NEXT X
7100 RETURN
10010 DATA 0,0,0,0,0,0,0,0,0,0,85,0,0,85,0,1
10020 DATA 105,64,1,105,64,5,170,80,5,170,80,6,170,144,6,170
10030 DATA 144,6,170,144,6,170,144,5,170,80,5,170,80,1,105,64
10040 DATA 1,105,64,0,85,0,0,85,0,0,0,0,0,0,0,131
10050 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,21
10060 DATA 85,84,21,85,84,90,170,165,90,170,165,106,170,169,106,170
10070 DATA 169,106,170,169,106,170,169,90,170,165,90,170,165,21,85,84
10080 DATA 21,85,84,0,0,0,0,0,0,0,0,0,0,0,0,131
30010 DATA 21,17,17,17,17,17,21,0
30020 DATA 4,4,4,4,4,4,4,0
30030 DATA 21,1,1,21,16,16,21,0
30040 DATA 21,1,1,5,1,1,21,0
30050 DATA 17,17,17,21,1,1,1,0
30060 DATA 21,16,16,21,1,1,21,0
30070 DATA 16,16,16,21,17,17,21,0
30080 DATA 21,1,1,1,1,1,1,0
30090 DATA 21,17,17,21,17,17,21,0
30100 DATA 21,17,17,21,1,1,1,0
